apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'

checkstyle {
    ignoreFailures = false
    configFile = rootProject.file('codequality/checkstyle.xml')
    sourceSets = [ project.sourceSets.main ]
    toolVersion = '7.3'
}

findbugs {
    ignoreFailures = false
    sourceSets = [ project.sourceSets.main ]
}

pmd {
    sourceSets = [project.sourceSets.main ]
    toolVersion = '5.4.0'
}

tasks.withType(FindBugs) {
    reports {
        xml.withMessages = true
    }
}

task findbugsHtml {
    group = "Verification"
    description = "Creates a human readable Findbugs report"
    doLast {
        ant.xslt(
                in: file("$buildDir/reports/findbugs/main.xml"),
                style: file('codequality/findbugs_default.xsl'),
                out: new File("$buildDir/reports/findbugs/main.html")
        )
    }
}
findbugsMain.finalizedBy findbugsHtml